project.version = 1.0

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }


    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.2'
        classpath 'org.ysb33r.gradle:vfs-gradle-plugin:1.0-beta1'
        classpath 'commons-httpclient:commons-httpclient:3.1'
    }
}

apply plugin: 'org.ysb33r.vfs'
apply plugin: 'org.asciidoctor.gradle.asciidoctor'


ext {
    deckjsVersion = 'master'
    asciidoctorBackendVersion = 'master'
    srcDir  = new File("$projectDir/src/asciidoc")
    srcDoc  = new File("$srcDir/slides.adoc")
    htmlDir = new File("$buildDir/docs")
    downloadDir = new File(buildDir,'download')
    templateDir = new File(downloadDir,'templates')
    deckjsDir   = new File(downloadDir,'deck.js')
}


task download << {
  mkdir downloadDir
  vfs {
    cp "zip:https://github.com/asciidoctor/asciidoctor-backends/archive/${asciidoctorBackendVersion}.zip!asciidoctor-backends-${asciidoctorBackendVersion}",
      templateDir, recursive:true, overwrite:true
    cp "zip:https://github.com/imakewebthings/deck.js/archive/${deckjsVersion}.zip!deck.js-${deckjsVersion}",
      deckjsDir, recursive:true, overwrite:true
  }

} 

download {
  outputs.dir templateDir
  outputs.dir deckjsDir
}

asciidoctor {
    logDocuments = true

    sourceDir 'src/asciidoc'

    sources {
        include 'slides.adoc'
    }

    backends 'deckjs'
//	backends 'html5'

    options template_dirs : [new File(templateDir,'haml').absolutePath ]
    options eruby: 'erubis'

    attributes 'source-highlighter': 'coderay'
    attributes idprefix: ''
    attributes idseparator: '-'

    resources {
        from (sourceDir) {
            include 'images/**'
        }
	from (downloadDir) {
		include 'deck.js/**'
	}
    }

    dependsOn download
}
